load("@buildifier_prebuilt//:rules.bzl", "buildifier")
load("@hedron_compile_commands//:refresh_compile_commands.bzl", "refresh_compile_commands")
load("@rules_multirun//:defs.bzl", "multirun")
load("@rules_shell//shell:sh_binary.bzl", "sh_binary")

buildifier(
    name = "buildifier.check",
    lint_mode = "warn",
    mode = "check",
    tags = ["manual"],
)

buildifier(
    name = "buildifier",
    lint_mode = "warn",
    mode = "fix",
    tags = ["manual"],
)

genrule(
    name = "gen-clang-format",
    outs = ["clang-format.bash"],
    cmd = """
echo "#!/usr/bin/env bash" > $@
echo "cd \\$$BUILD_WORKSPACE_DIRECTORY" >> $@
echo "exec bazelisk build \\$$@ //..." >> $@
""",
    tags = ["manual"],
)

sh_binary(
    name = "clang-format.check",
    srcs = ["clang-format.bash"],
    args = [
        "--aspects=@bazel_clang_format//:defs.bzl%check_aspect",
        "--output_groups=report",
        "--keep_going",
    ],
    tags = ["manual"],
)

sh_binary(
    name = "clang-format",
    srcs = ["clang-format.bash"],
    args = [
        "--aspects=@bazel_clang_format//:defs.bzl%fix_aspect",
        "--output_groups=report",
        "--keep_going",
        "--use_action_cache=false",
    ],
    tags = ["manual"],
)

multirun(
    name = "format.check",
    commands = [
        ":buildifier.check",
        ":clang-format.check",
    ],
    tags = ["manual"],
)

multirun(
    name = "format",
    commands = [
        ":buildifier",
        ":clang-format",
    ],
    tags = ["manual"],
)

genrule(
    name = "gen-clang-tidy",
    outs = ["clang-tidy.bash"],
    cmd = """
echo "#!/usr/bin/env bash" > $@
echo "cd \\$$BUILD_WORKSPACE_DIRECTORY" >> $@
echo "exec bazelisk build {options} \\$${{@:-//...}}" >> $@
""".format(
        options = " ".join([
            "--aspects=@rules_clang_tidy//:aspects.bzl%check",
            "--output_groups=report",
            "--keep_going",
            "--verbose_failures",
        ]),
    ),
    tags = ["manual"],
)

# lint all targets with default options
#  bazel run //tools:lint
#
# lint with a single check on a single target
#   bazel run //tools:lint -- \
#     --@rules_clang_tidy//:extra-options=--checks='-*,clang-analyzer-core.uninitialized.Assign' \
#     //test:point_test
#
sh_binary(
    name = "clang-tidy",
    srcs = ["clang-tidy.bash"],
    tags = ["manual"],
)

alias(
    name = "lint",
    actual = ":clang-tidy",
    tags = ["manual"],
)

refresh_compile_commands(
    name = "compile_commands",
    tags = ["manual"],
    targets = {
        "//sel/...": "--extra_toolchains=@llvm_toolchain//:all",
    },
)

genrule(
    name = "gen-coverage",
    srcs = ["@lcov//:genhtml"],
    outs = ["coverage.bash"],
    cmd = """
echo "#!/usr/bin/env bash" > $@
echo "set -euo pipefail" >> $@
echo "" >> $@
echo "cd \\$$BUILD_WORKSPACE_DIRECTORY" >> $@
echo "COVERAGE_DIR={cov_dir}" >> $@
echo "bazelisk coverage \\$${{@:-//...}}" >> $@
echo "$(execpath @lcov//:genhtml) {options} {report}" >> $@
echo "" >> $@
echo "echo wrote coverage report to" >> $@
echo "echo file://{html}" >> $@
""".format(
        cov_dir = "\\$$(bazelisk info output_path)/_coverage",
        html = "\\$${COVERAGE_DIR}/html/index.html",
        options = " ".join([
            "--output-directory \\$${COVERAGE_DIR}/html",
            "--show-details",
            "--function-coverage",
            "--branch-coverage",
            "--title zipline",
            "--highlight",
            "--legend",
            "--missed",
            "--demangle-cpp",
            "--dark-mode",
            "--rc genhtml_hi_limit=100",
        ]),
        report = "\\$${COVERAGE_DIR}/_coverage_report.dat",
    ),
    tags = ["manual"],
)

sh_binary(
    name = "coverage",
    srcs = [":coverage.bash"],
    args = [
        "--instrumentation_filter=//sel",
        "//...",
    ],
    data = ["@lcov//:genhtml"],
    tags = ["manual"],
)

_coverage_open_script = """
echo "#!/bin/bash" > $@
echo "set -euo pipefail" >> $@
echo "" >> $@
echo "cd \\$$BUILD_WORKSPACE_DIRECTORY" >> $@
echo "bazelisk run //tools:coverage" >> $@
echo "exec {{cmd}} {html}" >> $@
""".format(
    html = "\\$$(bazelisk info output_path)/_coverage/html/index.html",
)

# due to sh_binary tokenization, we can't use a generic open command and pass
# the path as an argument
# https://github.com/bazelbuild/bazel/issues/12313
genrule(
    name = "coverage.open",
    outs = ["coverage.open.bash"],
    cmd = select({
        "@platforms//os:macos": _coverage_open_script.format(cmd = "open"),
        "@platforms//os:linux": _coverage_open_script.format(cmd = "xdg-open"),
    }),
    executable = True,
    tags = ["manual"],
)
