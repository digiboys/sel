load("@buildifier_prebuilt//:rules.bzl", "buildifier")
load("@hedron_compile_commands//:refresh_compile_commands.bzl", "refresh_compile_commands")
load("@rules_multirun//:defs.bzl", "multirun")

buildifier(
    name = "buildifier.check",
    lint_mode = "warn",
    mode = "check",
    tags = ["manual"],
)

buildifier(
    name = "buildifier",
    lint_mode = "warn",
    mode = "fix",
    tags = ["manual"],
)

[
    genrule(
        name = name,
        outs = [name + ".bash"],
        cmd = """
echo "#!/usr/bin/env bash" > $@
echo "cd \\$$BUILD_WORKSPACE_DIRECTORY" >> $@
echo "exec bazelisk build {options} \\$${{@:-//...}}" >> $@
""".format(
            options = " ".join(
                options + [
                    "--@rules_multitransition//config:multitransition_test=False",
                    "--output_groups=report",
                    "--keep_going",
                ],
            ),
        ),
        executable = True,
        tags = ["manual"],
    )
    for name, options in [
        (
            "clang-format",
            [
                "--aspects=@bazel_clang_format//:defs.bzl%fix_aspect",
                "--use_action_cache=false",
            ],
        ),
        (
            "clang-format.check",
            [
                "--aspects=@bazel_clang_format//:defs.bzl%check_aspect",
            ],
        ),
        (
            "clang-tidy",
            [
                "--aspects=@rules_clang_tidy//:aspects.bzl%check",
                "--verbose_failures",
            ],
        ),
    ]
]

multirun(
    name = "format.check",
    commands = [
        ":buildifier.check",
        ":clang-format.check",
    ],
    tags = ["manual"],
)

multirun(
    name = "format",
    commands = [
        ":buildifier",
        ":clang-format",
    ],
    tags = ["manual"],
)

# lint all targets with default options
#  bazel run //tools:lint
#
# lint with a single check on a single target
#   bazel run //tools:lint -- \
#     --@rules_clang_tidy//:extra-options=--checks='-*,clang-analyzer-core.uninitialized.Assign' \
#     //test:point_test
#
alias(
    name = "lint",
    actual = ":clang-tidy",
    tags = ["manual"],
)

refresh_compile_commands(
    name = "compile_commands",
    tags = ["manual"],
    targets = {
        "//sel/...": " ".join([
            "--@rules_multitransition//config:multitransition_test=False",
            "--extra_toolchains=@llvm_toolchain//:all",
        ]),
    },
)

genrule(
    name = "coverage",
    srcs = ["@lcov//:genhtml"],
    outs = ["coverage.bash"],
    cmd = """
echo "#!/usr/bin/env bash" > $@
echo "set -euo pipefail" >> $@
echo "" >> $@
echo "cd \\$$BUILD_WORKSPACE_DIRECTORY" >> $@
echo "COVERAGE_DIR={cov_dir}" >> $@
echo "bazelisk coverage {coverage_opts} \\$${{@:-//...}}" >> $@
echo "$(execpath @lcov//:genhtml) {genhtml_opts} {report}" >> $@
echo "" >> $@
echo "echo wrote coverage report to" >> $@
echo "echo file://{html}" >> $@
""".format(
        cov_dir = "\\$$(bazelisk info output_path)/_coverage",
        coverage_opts = " ".join([
            "--@rules_multitransition//config:multitransition_test=False",
            "--instrumentation_filter=//sel",
        ]),
        genhtml_opts = " ".join([
            "--output-directory \\$${COVERAGE_DIR}/html",
            "--show-details",
            "--function-coverage",
            "--branch-coverage",
            "--title sel",
            "--highlight",
            "--legend",
            "--missed",
            "--demangle-cpp",
            "--dark-mode",
            "--rc genhtml_hi_limit=100",
        ]),
        html = "\\$${COVERAGE_DIR}/html/index.html",
        report = "\\$${COVERAGE_DIR}/_coverage_report.dat",
    ),
    executable = True,
    tags = ["manual"],
)

_coverage_open_script = """
echo "#!/usr/bin/env bash" > $@
echo "set -euo pipefail" >> $@
echo "" >> $@
echo "cd \\$$BUILD_WORKSPACE_DIRECTORY" >> $@
echo "bazelisk run //tools:coverage" >> $@
echo "exec {{cmd}} {html}" >> $@
""".format(
    html = "\\$$(bazelisk info output_path)/_coverage/html/index.html",
)

# due to sh_binary tokenization, we can't use a generic open command and pass
# the path as an argument
# https://github.com/bazelbuild/bazel/issues/12313
genrule(
    name = "coverage.open",
    outs = ["coverage.open.bash"],
    cmd = select({
        "@platforms//os:macos": _coverage_open_script.format(cmd = "open"),
        "@platforms//os:linux": _coverage_open_script.format(cmd = "xdg-open"),
    }),
    executable = True,
    tags = ["manual"],
)
